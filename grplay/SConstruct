# dependencies:
#    libzmq3-dev
#    libgtkmm-3.0-dev

# --release-build option
AddOption('--release-build', action='store_true', dest='release_build',	help='create optimized binary', default=False)

cxxflags = ['-std=c++14', '-Wall']  # common cxxflags

if GetOption('release_build'):
	cxxflags.extend(['-O2'])
else:
	cxxflags.extend(['-g', '-O0'])

env = Environment(
	CCFLAGS=cxxflags,
	LIBS=['pthread', 'boost_filesystem', 'boost_system', 'boost_thread',	'boost_log'],
	CPPDEFINES=['BOOST_SPIRIT_THREADSAFE', 'BOOST_LOG_DYN_LINK'],  # json support
	CPPPATH=['libs/']
)

env.ParseConfig('pkg-config --cflags --libs gtkmm-3.0 libzmq')

# static libzmqu library
zmqu_lib = env.StaticLibrary('zmqu', Glob('libs/zmqu/*.cpp'))

env.Program('grplay', [
	'grplay.cpp',
	'player_client.cpp',
	'library_tree_view.cpp',
	zmqu_lib
])

# tests
test_env = env.Clone()

test_env.Program(['test/treeview.cpp'])
