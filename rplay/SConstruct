# dependencies:
#	libgstreamer1.0-dev (>= 1.14.5)
#	libzmq3-dev (>= 4.2.5)
#	libasound2-dev (>= 1.1.3)
#	libboost-all-dev (>= 1.65) {filesystem, thread, log, date_time, program_options}
#	python2-dev (>= 2.7.17)

# --release-build option
AddOption('--release-build', action='store_true', dest='release_build',	help='create optimized binary', default=False)

cxxflags = ['-std=c++17', '-Wall',
	'-Wno-register'  # lots of python2 warnings
]  # common cxxflags

inotify_path = 'libs/inotify-cpp-0.2.0/src'


if GetOption('release_build'):
	cxxflags.extend(['-O2'])
else:
	cxxflags.extend(['-g', '-O0'])

env = Environment(
	CCFLAGS=cxxflags,
	LIBS=[
		'pthread',
		'boost_filesystem',
		'boost_system',
		'boost_thread',
		'boost_log',
		'boost_log_setup',
		'boost_date_time',
		'boost_program_options'],
	CPPDEFINES=['BOOST_SPIRIT_THREADSAFE', 'BOOST_LOG_DYN_LINK'],  # json support
	CPPPATH=['libs/', inotify_path + '/include']
)

env.ParseConfig('pkg-config --cflags --libs gstreamer-1.0 libzmq alsa python2')


def find_rplay_version():
	'''returns remoteplayer version as a string'''
	import subprocess, re, datetime, os

	if os.path.exists('version'):
		# grep it from debian/changelog
		pipe = subprocess.Popen(
			"cat version",
			stdout=subprocess.PIPE, shell=True)
		ver_line = pipe.communicate()[0].strip('\n')  # e.g. '1.0.6'
	else:
		ver_line = '0.0.0'

	return ver_line

def create_version_cpp(target, source, env):
	'''creates version.cpp surce file'''
	src = """/* this file is automatically generated by build script (see SConstruct), do not edit */
#include "version.hpp"

std::string software_build()
{
	return std::string{"%s"};
}

std::string software_version()
{
	return std::string{"%s"};
}

""" % ('%s.%s' % ('date', 'hash'), '1')  #git_version_descriptor(), find_rplay_version())

	with open(str(target[0]), 'w') as fout:
		fout.write(src)


# version (version.cpp)
def git_version_descriptor():
	'''returns pair (DATE, HASH) describes commit'''
	import subprocess, re, datetime

	# ask git for hash and date
	pipe = subprocess.Popen(
		'git;log;-1;--date=iso;--pretty=format:"%h %ad"'.split(';'), stdout=subprocess.PIPE)
	desc_line = pipe.communicate()[0]  # e.g. '"47b8e31 2017-03-06 13:01:38 +0100"'

	# parse git's answer
	ma = re.match(r'"(\w{7}) (\d{4})-(\d{2})-(\d{2}) \d{2}:\d{2}:\d{2} \+\d{4}"', desc_line)
	short_hash, year, mounth, day = ma.group(1, 2, 3, 4)

	d = datetime.date(int(year), int(mounth), int(day))

	return (d.strftime('%Y%m%d'), short_hash)


version_cpp = env.Command('version.cpp', '', create_version_cpp)
AlwaysBuild(version_cpp)

version_obj = env.Object('version.cpp')
Requires(version_obj, version_cpp)

env.Append(LINKFLAGS=str(version_obj[0]))


# static libzmqu library
zmqu_lib = env.StaticLibrary('zmqu', Glob('libs/zmqu/*.cpp'))

# static inotify-cpp
inotify_lib = env.StaticLibrary('inotify', Glob(inotify_path + '/*.cpp'))

# static rp library
rp_lib = env.StaticLibrary('rp', Glob('libs/rplib/*.cpp'))

common_objs = env.Object([
	'gst_audio_player.cpp',
	'player.cpp',
	'playlist.cpp',
	'library.cpp',
	'downloader.cpp',
	'config.cpp',
	'io.cpp',
	'json.cpp',
	'fs.cpp',
	'zmq_interface.cpp',
	'volume_control.cpp',
	'youtube_dl.cpp'
]);

Requires(common_objs, version_obj)


rplay = env.Program('rplay', [
	'rplay.cpp',
	common_objs,
	rp_lib,
	zmqu_lib,
	inotify_lib
])

# install support
rplay_target_path = '/opt/remoteplayer'
env.Install(rplay_target_path, rplay)
env.Alias('install', rplay_target_path)
